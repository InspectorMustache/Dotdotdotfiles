#!/bin/fish
set base_vars BASE00 BASE01 BASE02 BASE03 BASE04 BASE05 BASE06 BASE07 \
              BASE08 BASE09 BASE0A BASE0B BASE0C BASE0D BASE0E BASE0F
set required_vars REAL_NAME MAIL_ADDRESS HOME $base_vars

# add base variables with '#' prepended
for var in $base_vars
    set {$var}_HASH '#'$$var
    set required_vars $required_vars {$var}_HASH
end

function merge_files -a skel_file custom_path out_path
    set -l skel_path $HOME/.local/share/skel/$skel_file.skel
    test ! -e $skel_path; and return

    set -l skel_lines
    cat $skel_path | while read -l line
        set skel_lines $skel_lines $line
    end

    set -l custom_lines
    # if there's no custom_path, an empty custom_lines variable will just result in a copy of the skel_path, which is okay
    if test -e $custom_path
        cat $custom_path | while read -l line
            set custom_lines $custom_lines $line
        end
    end

    mkdir -p (dirname $out_path)
    echo -n "" > $out_path
    for line in $skel_lines
        echo $line >> $out_path
        string match -qe '{{{custom}}}' $line; and begin
            string join \n $custom_lines >> $out_path
        end; or true # so this doesn't create an exit code of 1 even though nothing's wrong
    end
end

function test_vars
    for var in $required_vars
        if not set -q $var
            echo "Variable $var not set for merging based on variables."
            return 1
        end
    end
end

function merge_var -a skel_file out_path
    set -l skel_path $HOME/.local/share/skel/$skel_file.skel
    test ! -e $skel_path; and return

    mkdir -p (dirname $out_path)
    echo -n "" > $out_path
    set skel_file_content (cat $skel_path)
    for var in $required_vars
        set skel_file_content (string replace '{{{'$var'}}}' "$$var" $skel_file_content)
    end
    string join \n $skel_file_content > $out_path
end

# merge configs that have dedicated custom files
merge_files i3blocks $HOME/.config/i3blocks/custom $HOME/.config/i3blocks/config
merge_files i3 $HOME/.config/i3/custom $HOME/.config/i3/config

# merge vars into configs
test_vars; or exit
merge_var mbsync $HOME/.mbsyncrc
merge_var msmtp $HOME/.msmtprc
merge_var neomutt $HOME/.config/neomutt/neomuttrc
merge_var notmuch-config $HOME/.config/notmuch/notmuch-config
merge_var mpv $HOME/.config/mpv/mpv.conf
merge_var fish-variables $HOME/.config/fish/functions/__fish_set_universal_variables.fish
